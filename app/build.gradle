buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        // Actualiza aquí la versión del plugin de Android a 8.3.1
        classpath 'com.android.tools.build:gradle:8.3.1'
    }
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf' version '0.9.4'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }

    plugins {
        // Plugin gRPC para generar stubs Android-lite
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.56.1"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

android {
    namespace 'eina.unizar.cliente_movil'
    compileSdk 34

    defaultConfig {
        applicationId "eina.unizar.cliente_movil"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*sourceSets {
        debug {
            java.srcDirs += 'build/generated/source/proto/debug/java'
            java.srcDirs += 'build/generated/source/proto/debug/grpc'
        }
        release {
            java.srcDirs += 'build/generated/source/proto/release/java'
            java.srcDirs += 'build/generated/source/proto/release/grpc'
        }
        // You can add other variants similarly
    }*/

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            shrinkResources false
            //minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

// Ensure proto generation runs before Kotlin compilation for each variant
afterEvaluate {
    android.applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def compileTask = tasks.named("compile${variantName}Kotlin")
        def protoTask   = tasks.named("generate${variantName}Proto")

        compileTask.configure {
            dependsOn(protoTask)
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // WebSocket para la comunicación con el servidor
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'

    // Gson para el manejo de JSON
    implementation 'com.google.code.gson:gson:2.10.1'

    // Socket.IO for WebSocket communication
    implementation('io.socket:socket.io-client:2.1.0') {
        exclude group: 'org.json', module: 'json'
    }
    implementation 'org.json:json:20220924'

    implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    // Coroutines para operaciones asíncronas
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Protobuf y gRPC
    implementation 'com.google.protobuf:protobuf-javalite:3.25.1'
    implementation 'com.google.protobuf:protobuf-kotlin-lite:3.25.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.56.1'
    implementation 'io.grpc:grpc-stub:1.56.1'
    implementation 'io.grpc:grpc-okhttp:1.56.1' // Para Android


    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}